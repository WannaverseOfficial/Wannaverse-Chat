plugins {
    alias(libs.plugins.java)
    alias(libs.plugins.spring.boot) apply false
    alias(libs.plugins.spring.dependency.management) apply false
    alias(libs.plugins.spotless)
}

repositories {
    mavenCentral()
}

subprojects {
    apply plugin: "java"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "com.diffplug.spotless"

    group = "com.wannaverse"
    version = "0.0.0"

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation platform(libs.slf4j.bom)
        implementation platform(libs.jackson.bom)

        implementation libs.bundles.jackson
        implementation libs.spring.openapi

        compileOnly libs.lombok
        annotationProcessor libs.lombok
    }

    spotless {
        java {
            importOrder()
            googleJavaFormat()
                    .aosp()
                    .reflowLongStrings()
                    .formatJavadoc(true)
                    .reorderImports(true)
                    .groupArtifact('com.google.googlejavaformat:google-java-format')
            trimTrailingWhitespace()
            leadingTabsToSpaces()
            endWithNewline()
        }
        groovyGradle {
            trimTrailingWhitespace()
            leadingTabsToSpaces()
            endWithNewline()
        }
    }
}

spotless {
    groovyGradle {
        trimTrailingWhitespace()
        leadingTabsToSpaces()
        endWithNewline()
    }
}
